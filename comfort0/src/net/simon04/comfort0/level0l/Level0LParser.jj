options {
  STATIC = false;
  OUTPUT_DIRECTORY = "parsergen";
}

PARSER_BEGIN(Level0LParser)
package net.simon04.comfort0.level0l.parsergen;
import org.openstreetmap.josm.data.osm.*;
import org.openstreetmap.josm.data.coor.*;
public class Level0LParser {
}
PARSER_END(Level0LParser)

<DEFAULT> //HEADER
SKIP: { " " }

<DEFAULT> //HEADER
TOKEN: {
    <H_EOL: ("\n" | "\r" | "\r\n") > : DATA
    | <H_ID: (["0"-"9"])+ >
    | <NODE: "node" >
    | <WAY: "way" >
    | <RELATION: "relation" >
    | <FLOAT: ("+" | "-")? (["0"-"9"])+ ("." (["0"-"9"])+)? >
    | <COLON: ":" >
    | <COMMA: "," >
    | <COMMENT_START: "#"> : COMMENT
}

<COMMENT>
SKIP: { < ~[] > }

<COMMENT>
TOKEN: {
    <C_EOL: ("\n" | "\r" | "\r\n") > : DATA
}

<DATA>
TOKEN: {
    <D_EOL: ("\n" | "\r" | "\r\n") >
    | <D_SPACE: " " >
    | <D_ID: (["0"-"9"])+ >
    | <WY: "wy" >
    | <ND: "nd" >
    | <REL: "rel" >
    | <EQ: "=" > : VALUE
    | <IDENT: ["a"-"z", "A"-"Z", "_"] (["a"-"z", "A"-"Z", "_", "-", "0"-"9"])* >
    | <D_COMMENT_START: "#"> : COMMENT
}

<VALUE>
TOKEN: {
    <V_EOL: ("\n" | "\r" | "\r\n") > : DATA
    | <TEXT: (~["\n", "\r"])+ >
}

NodeData node():
{
    NodeData r;
    Token id, lat, lon;
}
{
    <NODE>
    { r = new NodeData(); }
    id=<H_ID>
    { r.setId(Long.parseLong(id.image)); }
    <COLON>
    lat=<FLOAT>
    <COMMA>
    lon=<FLOAT>
    { r.setCoor(new LatLon(Double.parseDouble(lat.image), Double.parseDouble(lon.image))); }
    ( <COMMENT_START> | <H_EOL> )
    ( tag(r) )*
    { return r; }
}

WayData way():
{
    WayData r;
    Token id;
}
{
    <WAY>
    { r = new WayData(); }
    id=<H_ID>
    { r.setId(Long.parseLong(id.image)); }
    ( <COMMENT_START> | <H_EOL> )
    way_data(r)
    { return r; }
}

void way_data(WayData r):
{}
{
    LOOKAHEAD(3)
    ( way_node(r) way_data(r) ) // TODO avoid recursive call to way_data
    |
    ( tag(r) )*
}

void way_node(WayData r):
{
    Token id;
}
{
    <D_SPACE> <D_SPACE>
    <ND>
    ( <D_SPACE> )+
    id=<D_ID>
    { r.getNodeIds().add(Long.parseLong(id.image)); }
    ( <D_SPACE> )*
    ( <D_COMMENT_START> | <D_EOL> )
}

void tag(PrimitiveData r):
{
    Token k, v;
}
{
    <D_SPACE> <D_SPACE>
    k=<IDENT>
    ( <D_SPACE> ) *
    <EQ>
    v=<TEXT>
    <V_EOL>
    { r.put(k.image.trim(), v.image.trim()); }
}
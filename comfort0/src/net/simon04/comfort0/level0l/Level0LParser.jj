options {
  STATIC = false;
  OUTPUT_DIRECTORY = "parsergen";
}

PARSER_BEGIN(Level0LParser)
package net.simon04.comfort0.level0l.parsergen;
import org.openstreetmap.josm.data.osm.*;
import org.openstreetmap.josm.data.coor.*;
import java.util.ArrayList;
import java.util.List;
public class Level0LParser {
}
PARSER_END(Level0LParser)

<DEFAULT> //HEADER
SKIP: { " " }

<DEFAULT> //HEADER
TOKEN: {
    <H_EOL: ("\n" | "\r" | "\r\n") > : DATA
    | <H_ID: (["0"-"9"])+ >
    | <NODE: "node" >
    | <WAY: "way" >
    | <RELATION: "relation" >
    | <FLOAT: ("+" | "-")? (["0"-"9"])+ ("." (["0"-"9"])+)? >
    | <COLON: ":" >
    | <COMMA: "," >
    | <COMMENT_START: "#"> : COMMENT
}

<COMMENT>
SKIP: { < ~["\n", "\r"] > }

<COMMENT>
TOKEN: {
    <C_EOL: ("\n" | "\r" | "\r\n") > : DATA
}

<DATA>
TOKEN: {
    <D_EOL: ("\n" | "\r" | "\r\n") >
    | <D_SPACE: " " >
    | <D_ID: (["0"-"9"])+ >
    | <WY: "wy" >
    | <ND: "nd" >
    | <REL: "rel" >
    | <EQ: "=" > : VALUE
    | <IDENT: ["a"-"z", "A"-"Z", "_"] (["a"-"z", "A"-"Z", "_", "-", "0"-"9"])* >
    | <D_COMMENT_START: "#"> : COMMENT
}

<VALUE>
TOKEN: {
    <V_EOL: ("\n" | "\r" | "\r\n") > : DATA
    | <TEXT: (~["\n", "\r"])+ >
}

List<PrimitiveData> primitives():
{
    List<PrimitiveData> primitives = new ArrayList<PrimitiveData>();
    PrimitiveData p;
}
{
    (
        ( p = node() | p = way() | p = relation() )
        { primitives.add(p); }
    )*
    <EOF>
    { return primitives; }
}

NodeData node():
{
    NodeData r;
    Token id, lat, lon;
}
{
    <NODE>
    { r = new NodeData(); }
    id=<H_ID>
    { r.setId(Long.parseLong(id.image)); }
    <COLON>
    lat=<FLOAT>
    <COMMA>
    lon=<FLOAT>
    { r.setCoor(new LatLon(Double.parseDouble(lat.image), Double.parseDouble(lon.image))); }
    ( <COMMENT_START> <C_EOL> | <H_EOL> )
    ( <D_SPACE> <D_SPACE> tag(r) )*
    { return r; }
}

WayData way():
{
    WayData r;
    Token id;
}
{
    <WAY>
    { r = new WayData(); }
    id=<H_ID>
    { r.setId(Long.parseLong(id.image)); }
    ( <COMMENT_START> <C_EOL> | <H_EOL> )
    way_data(r)
    { return r; }
}

RelationData relation():
{
    RelationData r;
    Token id;
}
{
    <RELATION>
    { r = new RelationData(); }
    id=<H_ID>
    { r.setId(Long.parseLong(id.image)); }
    ( <COMMENT_START> <C_EOL> | <H_EOL> )
    relation_data(r)
    { return r; }
}

void way_data(WayData r):
{}
{
    (
        <D_SPACE> <D_SPACE>
        ( way_node(r) | tag(r) )
    )*
}

void way_node(WayData r):
{
    Token id;
}
{
    <ND>
    ( <D_SPACE> )+
    id=<D_ID>
    { r.getNodeIds().add(Long.parseLong(id.image)); }
    ( <D_SPACE> )*
    ( <D_COMMENT_START> <C_EOL> | <D_EOL> )
}

void relation_data(RelationData r):
{}
{
    (
        <D_SPACE> <D_SPACE>
        ( relation_member(r) | tag(r) )
    )*
}

void relation_member(RelationData r):
{
    OsmPrimitiveType type;
    Token id;
    Token role = null;
}
{
    (
        <ND> { type = OsmPrimitiveType.NODE; }
    |
        <WY> { type = OsmPrimitiveType.WAY; }
    |
        <REL> { type = OsmPrimitiveType.RELATION; }
    )
    <D_SPACE>
    id=<D_ID>
    (
        <D_SPACE>
        (
            role=<IDENT> ( <D_SPACE> )*
        |
            ( <D_SPACE> )*
        )
    )?
    { r.getMembers().add(new RelationMemberData(role != null ? role.image : "", type, Long.parseLong(id.image))); }
    ( <D_COMMENT_START> <C_EOL> | <D_EOL> )
}

void tag(PrimitiveData r):
{
    Token k, v;
}
{
    k=<IDENT>
    ( <D_SPACE> ) *
    <EQ>
    v=<TEXT>
    <V_EOL>
    { r.put(k.image.trim(), v.image.trim()); }
}
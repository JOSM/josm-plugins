import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
  id 'java'
  id 'eclipse'
  id 'jacoco'
  id 'pmd'
  alias(libs.plugins.spotless)
  alias(libs.plugins.versions)
  alias(libs.plugins.errorprone)
  alias(libs.plugins.markdown)
  alias(libs.plugins.javafx)
  alias(libs.plugins.sonarqube)
  alias(libs.plugins.spotbugs)
  alias(libs.plugins.josm)
}

apply from: 'gradle/tool-config.gradle'
// TODO: repair Mapillary markdown task
// error with subdir build/markdown
//apply from: 'gradle/markdown.gradle'

sourceCompatibility = '21'

def versionProcess = new ProcessBuilder("git", "describe", "--always", "--dirty").start()
versionProcess.waitFor()
if (versionProcess.exitValue() != 0) {
  logger.error("Could not determine the current version of this JOSM plugin!")
  version = "‹unknown›"
} else {
  version = versionProcess.in.text.trim()
}
archivesBaseName = 'MicrosoftStreetside'

repositories {
  mavenCentral()
  maven {
    url "https://josm.openstreetmap.de/nexus/content/repositories/releases/"
  }
}

javafx {
    modules = [ 'javafx.graphics', 'javafx.controls', 'javafx.swing' ]
}

dependencies {
  errorprone(libs.errorprone)
  spotbugsPlugins(libs.findsecbugsPlugin)
  testRuntimeOnly libs.junit.jupiter.engine
  testImplementation libs.bundles.junit
  testImplementation libs.josm.unittest
  testImplementation libs.wiremock
  testImplementation libs.jmockit
  testImplementation libs.awaitility
}

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }
    resources {
      srcDirs = ["$projectDir"]
      include 'data/**'
      include 'images/**'
      include 'LICENSE'
      include 'LICENSE_*'
    }
  }
  test {
    java {
      srcDirs = ['test/unit']
    }
    resources{
      srcDirs = ['test/data']
    }
  }
}

// Spotbugs config
spotbugs {
    toolVersion = libs.versions.spotbugs.get()
    ignoreFailures = true
    effort = Effort.valueOf('MAX')
    reportLevel = Confidence.valueOf('LOW')
    //sourceSets = [sourceSets.main, sourceSets.test]
}

spotless {
  enforceCheck = false
  format("misc") {
    target("**/*.gradle", "**.*.md", "**/.gitignore")

    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
  }
  java {
    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
    removeUnusedImports()
  }
}

josm {
  debugPort = 7051
}

eclipse {
  project {
    name = 'MicrosoftStreetside'
    comment = josm.manifest.description
    natures 'org.sonarlint.eclipse.core.sonarlintNature', 'ch.acanda.eclipse.pmd.builder.PMDNature', 'org.eclipse.buildship.core.gradleprojectnature'
    buildCommand 'org.sonarlint.eclipse.core.sonarlintBuilder'
    buildCommand 'ch.acanda.eclipse.pmd.builder.PMDBuilder'
    buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
  }
}
eclipseClasspath.dependsOn cleanEclipseClasspath
eclipseProject.dependsOn cleanEclipseProject
tasks.eclipse.dependsOn = ['eclipseClasspath', 'eclipseProject']

tasks.withType(JavaCompile) {
  // Character encoding of Java files
  options.encoding = 'UTF-8'
}
tasks.withType(Javadoc) {
  failOnError false
}
tasks.withType(SpotBugsTask) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

test {
  project.afterEvaluate {
    jvmArgs("-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}")
  }
  useJUnitPlatform()
  testLogging {
    exceptionFormat "full"
    events TestLogEvent.FAILED, TestLogEvent.SKIPPED
    showCauses true

    info {
      events TestLogEvent.STARTED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR
      showStandardStreams = true
    }
  }
}
